#!/usr/bin/env perl

use 5.010;
use warnings;
use strict;
use autodie;

use LWP::UserAgent;
use JSON qw(decode_json);
use Date::Parse;
use Date::Format;
use FindBin;

my $api_key = shift;

my $outfile = "$FindBin::Bin/../src/plus.html";
my $rocket  = "$FindBin::Bin/rocket";

my $ua = LWP::UserAgent->new;
my $res = $ua->get("https://www.googleapis.com/plus/v1/people/101415443691492553003/activities/public?key=$api_key");
die "plus feed fetch failed: ".$res->status_line unless $res->is_success;

my $data = decode_json($res->content);

open my $out, ">:encoding(UTF-8)", $outfile;

for my $item (splice @{$data->{items}}, 0, 6) {
    next if $item->{kind} ne "plus#activity";

    my $postURL = $item->{url};

    my $time = time2str("%e %b at %R UTC", str2time($item->{published}), 'UTC');

    my ($title) = $item->{title} =~ m/^(.*?)$/m;
    $title = $item->{object}->{attachments}->[0]->{displayName} // "" unless $title;

    my $object = $item->{verb} eq 'share' ? $item->{object} : $item;
    my $author    = $object->{actor}->{displayName};
    my $authorURL = $object->{actor}->{url};

    print $out <<END
<div class='plus-post'>
  <span class='plus-post-meta'><a href='$authorURL' target='_blank' class='plus-post-author'>+$author</a> on <a href='$postURL' target='_blank' class='plus-post-date'>$time</a></span>
  <p class='plus-post-title'>$title</p>
</div>
END
;

}

close $out;
